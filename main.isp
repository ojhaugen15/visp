
a character_width 7.8
a font_size 13
a font_color 'purple'
a font_style 'Courier'
a editor_background 'black'
a body_padding 10
a breathing_room 10
a storage_name 'vispojhaugen'
a cursor_opacity '0.4'
a space_code '&nbsp;'
a left_bracket '&lt;'
a right_bracket '&gt;'
a and_symbol '&amp;'
a cursor_padding 8
a interior_program 'programojhaugen'

a command_mode true
a editing_mode false
a undo_mode false
a nonline_elements 2
a selection_start 0
a selection_end 0
a copied_text ''
a search_mode false
a search_term ''
a function_lookup 0
a undone_state ''
a previous_state ''

a body_node :getValue document 'body';
a body_style :getValue body_node 'style';
setValue body_style 'background' editor_background
setValue body_style 'color' font_color
setValue body_style 'fontSize' font_size
setValue body_style 'fontFamily' font_style
setValue body_style 'fontWeight' 'bold'

a cursor_element :document.createElement 'div';
a cursor_style :getValue cursor_element 'style';
body_node.appendChild cursor_element
setValue cursor_style 'height' font_size
setValue cursor_style 'width' character_width
setValue cursor_style 'background' font_color
setValue cursor_style 'opacity' cursor_opacity
setValue cursor_style 'position' 'absolute'
a current_x 0
a current_y 0
setCursor

a current_line :addLine;
body_node.addEventListener 'keydown' executeAction
body_node.addEventListener 'keyup' secondaryAction

:restartUndo
:a previous_state :getContent;
:a undo_mode false

:addLine addAfter
:a new_line :document.createElement 'div';
:setValue new_line 'textContent'
:a line_style :getValue new_line 'style';
:setValue line_style 'width' '100%'
:setValue line_style 'height' font_size
:i :arentSame addAfter undefined;
::a editorContent :getValue body_node 'children';
::a targetElement :getValue editorContent :addNumbers addAfter nonline_elements;;
::targetElement.after new_line
::e new_line
:body_node.appendChild new_line
:e new_line

:setCursor
:setValue cursor_style 'top' :addNumbers :multiplyNumbers font_size current_y; cursor_padding;
:setValue cursor_style 'left' :addNumbers :multiplyNumbers character_width current_x; cursor_padding;

:executeAction keyEvent
:a keyPressed :getValue keyEvent 'key';
:i :keyMuted keyPressed;
::e
:i :areSame search_mode true;
::i :areSame keyPressed 'Enter';
:::a nextIndex :findNext search_term current_y;
:::a current_x 0
:::a current_y nextIndex
:::setCursor
:::updateScroll
:::e
::o search_term keyPressed
::e
:i :areSame command_mode false;
::i :areSame keyPressed 'Enter';
:::executeEnter
:::e
::i :areSame keyPressed 'Backspace';
:::executeBackspace
:::e
::i :areSame keyPressed ' ';
:::keyEvent.preventDefault
:::insertCharacter ' '
:::e
::insertCharacter keyPressed
::e
:executeCommand keyPressed

:keyMuted keyPressed
:i :areSame keyPressed 'Meta';
::e true
:i :areSame keyPressed 'Escape';
::e true
:i :areSame keyPressed 'Alt';
::e true
:i :areSame keyPressed 'Control';
::e true
:i :areSame keyPressed 'Shift';
::e true
:i :areSame keyPressed 'CapsLock';
::e true
:i :areSame keyPressed 'Tab';
::e true

:getLine lineIndex
:a editorContent :getValue body_node 'children';
:e :getValue editorContent :addNumbers lineIndex nonline_elements;;

:executeEnter
:a line1 :getValue current_line 'textContent';
:a last1 line1
:a searchIndex 0
:a searchTarget :differenceNumbers current_x 1;
:i :arentSame searchTarget searchIndex;
::a last1 :y last1;
::a searchIndex :addNumbers searchIndex 1;
:a line2 :y last1;
:y last1 null line1
:i :areSame current_x 0;
::a line2 line1
::a line1 ''
::a current_x null
:a newLine :addLine current_y;
:setValue current_line 'innerHTML' :toHTML line1;
:setValue newLine 'innerHTML' :toHTML line2;
:a current_y :addNumbers current_y 1;
:a current_x 0
:setCursor
:a current_line newLine

:executeBackspace 
:i :areSame current_x 0;
::i :areSame current_y 0;
:::e
::a lineValue :getValue current_line 'textContent';
::body_node.removeChild current_line
::a current_y :differenceNumbers current_y 1;
::a current_line :getLine current_y;
::a otherValue :getValue current_line 'textContent';
::a current_x :getLength otherValue;
::setValue current_line 'innerHTML' :toHTML :o otherValue lineValue;;
::setCursor
::e
:a lineValue :getValue current_line 'textContent';
:i :areSame current_x 1;
::a lineValue :y lineValue;
::setValue current_line 'innerHTML' :toHTML lineValue;
::a current_x :differenceNumbers current_x 1;
::setCursor
::e
:a replacementIndex :differenceNumbers current_x 2;
:a keyNode lineValue
:a searchIndex 0
:i :arentSame keyNode null;
::i :areSame replacementIndex searchIndex;
:::a operativeNode keyNode
:::a keyNode null
:::a replacementIndex null
::a searchIndex :addNumbers searchIndex 1;
::a keyNode :y keyNode;
:y operativeNode :y :y operativeNode;;
:setValue current_line 'innerHTML' :toHTML lineValue;
:a current_x :differenceNumbers current_x 1;
:setCursor

:toHTML stringValue
:a currentNode :o stringValue;
:i :arentSame :u currentNode; null;
::a currentCharacter :u currentNode;
::i :areSame currentCharacter ' ';
:::a spaceCode space_code
:::y previousNode spaceCode
:::y spaceCode :y currentNode; stringValue
:::a currentCharacter null
::i :areSame currentCharacter '<';
:::a leftBracket left_bracket
:::y previousNode leftBracket
:::y leftBracket :y currentNode; stringValue
:::a currentCharacter null
::i :areSame currentCharacter '>';
:::a rightBracket right_bracket
:::y previousNode rightBracket
:::y rightBracket :y currentNode; stringValue
:::a currentCharacter null
::i :areSame currentCharacter '&';
:::a andSymbol and_symbol
:::y previousNode andSymbol
:::y andSymbol :y currentNode; stringValue
:::a currentCharacter null
::a previousNode currentNode;
::a currentNode :y currentNode;
:e stringValue

:getContent
:a searchIndex nonline_elements
:a editorContent :getValue :body_node 'children';;
:a editorSize :getValue editorContent 'length';
:a outputString ''
:i :firstGreater editorSize searchIndex;
::a lineContent :getValue :getValue editorContent searchIndex; 'textContent';
::o lineContent '\n'
::o outputString lineContent
::a searchIndex :addNumbers searchIndex 1;
:e outputString

:clearContent
:a editorContent :getValue body_node 'children';
:a currentLine :getValue editorContent nonline_elements;
:i :arentSame currentLine undefined;
::body_node.removeChild currentLine
::a currentLine :getValue editorContent nonline_elements;

:setContent textContent
:a newLine ''
:a searchIndex 0
:a contentSize :getLength textContent;
:i :firstGreater contentSize searchIndex;
::a currentCharacter :getValue textContent searchIndex;
::i :areSame currentCharacter '\n';
:::a nextCharacter :getValue textContent :addNumbers searchIndex 1;;
:::i :arentSame nextCharacter "'";
::::i :arentSame nextCharacter '"';
:::::a createdLine :addLine;
:::::setValue createdLine 'innerHTML' :toHTML newLine;
:::::a newLine ''
:::::a currentCharacter ''
:::::a nextCharacter ''
::::a nextCharacter ''
:::a currentCharacter ''
::o newLine currentCharacter
::a searchIndex :addNumbers searchIndex 1;
:a current_x 0
:a current_y :differenceNumbers :getValue :getValue body_node 'children'; 'length'; :addNumbers nonline_elements 1;;
:setCursor
:updateScroll
:current_line createdLine

:getSize
:e :differenceNumbers :getValue :getValue body_node 'children'; 'length'; nonline_elements;

:insertCharacter keyPressed
:a lineValue :getValue current_line 'textContent';
:a lineSize :getLength lineValue;
:i :areSame lineSize current_x;
::setValue current_line 'innerHTML' :toHTML :o lineValue keyPressed;;
::a current_x :addNumbers current_x 1;
::setCursor
::e
:a searchIndex 0
:a searchTarget :differenceNumbers current_x 1;
:a currentNode :o lineValue;
:i :arentSame currentNode null;
::i :areSame searchTarget searchIndex;
:::a stringEnd :y :y currentNode;;
:::o keyPressed stringEnd
:::y currentNode keyPressed lineValue
:::setValue current_line 'innerHTML' :toHTML lineValue;
:::a current_x :addNumbers current_x 1;
:::setCursor 
:::e
::a currentNode :y currentNode;

:lineJump pagePercentage goBackwards
:a jumpSize :roundNumber :quotientNumbers :multiplyNumbers pagePercentage window.innerHeight; font_size;;
:i :areSame goBackwards true;
::e :differenceNumbers current_y jumpSize;
:e :addNumbers current_y jumpSize;

:highlightLine lineIndex
:a targetLine :getLine lineIndex;
:a lineStyle :a targetLine 'style';
:a lineStyle 'background' 'purple'
:a lineStyle 'color' 'black'

:unhighlightLine lineIndex
:a targetLine :getLine lineIndex;
:a lineStyle :a targetLine 'style';
:a lineStyle 'background' null
:a lineStyle 'color' null

:copySelection
:a outputString ''
:a searchIndex selection_start
:i :arentSame :firstGreater searchIndex selection_end; true;
::o outputString :getValue :getLine searchIndex; 'textContent';
::o outputString '\n'
::a searchIndex :addNumbers searchIndex 1;
:a copied_text outputString

:removeSelection
:a searchIndex selection_end
:i :arentSame :firstGreater selection_start searchIndex; true;
::targetLine :getLine searchIndex;
::body_node.removeChild targetLine
::a searchIndex :differenceNumbers searchIndex 1;
:a current_x 0
:a current_y selection_start
:setCursor
:i :areSame current_y :getSize;;
::a current_line addLine
::e
:a current_line :getLine current_y;

:pasteSelection inheritedNode
:a currentNode :o copied_text;
:i :arentSame inheritedNode undefined;
::a currentNode inheritedNode
::a inheritedNode undefined
:a currentLine currentNode
:i :arentSame currentNode null;
::i :areSame :u currentNode; '\n';
:::y previousNode null currentLine
:::a createdLine :addLine current_y;
:::setValue createdLine 'innerHTML' :toHTML currentLine;
:::pasteSelection :y currentNode;
:::e
::a previousNode currentNode
::a currentNode :y currentNode;

:executeCommand keyPressed
:i :areSame keyPressed 'i';
::restartUndo
::a command_mode false
::e
:i :areSame keyPressed ';';
::a newProgram :getContent;
::a oldProgram :getElement interior_program;
::i :arentSame oldProgram undefined;
:::body_node.removeChild oldProgram
::nonline_elements 3
::a programElement :document.createElement 'script';
::setValue 'text' newProgram
::setValue 'id' interior_program
::a firstElement :getValue :getValue body_node 'children'; 0;
::firstElement.after programElement
::e
:i :areSame keyPressed ':';
::a search_mode true
::e
:i :areSame keyPressed 'f';
::a functionName :currentIsland;
::a functionLocation :findNext :o ':' functionName; function_lookup;
::i :areSame current_y functionLocation;
:::a function_lookup 0
:::e
::a current_y functionLocation
::a current_y function_lookup
::a current_x 0
::setCursor
::updateScroll
::a current_line :getLine current_y;
::e
:i :areSame keyPressed 'v';
::a editing_mode true
::a selection_start current_y
::a selection_end current_y
::highlightLine current_y
::e
:i :areSame keyPressed 'p';
::restartUndo
::pasteSelection
::e
:i :areSame keyPressed 'x';
::restartUndo
::copySelection
::removeSelection
::e
:i :areSame keyPressed 'c';
::copySelection
::e
:i :areSame keyPressed 'r';
::i :arentSame undo_mode true;
:::e
::restartUndo
::a previous_state :getContent;
::clearContent
::setContent undone_state
::e
:i :areSame keyPressed 'u';
::i :areSame undo_mode true;
:::e
::a undone_state :getContent;
::clearContent
::setContent previous_state
::a undo_mode true
::e
:i :areSame keyPressed 'w';
::i :areSame current_x 0;
:::e
::i :areSame current_x 0;
:::e
::a lineContent :getValue current_line 'textContent';
::a searchIndex 0
::a currentNode :o lineContent;
::a lastBreak 0
::i :arentSame currentNode null;
:::i :areSame searchIndex current_x;
::::a current_x :addNumbers lastBreak 1;
::::setCursor
::::e
:::i :areSame :u currentNode; ' ';
::::a lastBreak searchIndex
:::a searchIndex :addNumbers searchIndex 1;
:::a currentNode :y currentNode;
::e
:i :areSame keyPressed 'e';
::i :areSame current_x 0;
:::e
::a lineContent :getValue current_line 'textContent';
::a searchIndex 0
::a currentNode :o lineContent;
::a lastBreak 0
::i :arentSame currentNode null;
:::i :areSame searchIndex current_x;
::::a current_x lastBreak
::::setCursor
::::e
:::i :areSame :u currentNode; ' ';
::::a lastBreak searchIndex
:::a searchIndex :addNumbers searchIndex 1;
:::a currentNode :y currentNode;
::e
:i :areSame keyPressed 'b';
::a lineContent :getValue current_line 'textContent';
::a lineSize :getLength lineContent;
::a currentIndex current_x
::a currentNode :o lineContent;
::i :arentSame currentNode null;
:::i :areSame :u currentNode; ' ';
::::a current_x currentIndex
::::setCursor
::::e
:::a currentIndex :addNumbers currentIndex 1;
:::a currentNode :y currentNode;
::a current_x currentIndex
::setCursor
::e
:i :areSame keyPressed 'a';
::a current_x 0
::setCursor
::e
:i :areSame keyPressed 'y';
::a current_x :getLength :getValue current_line 'textContent;;
::setCursor
::e
:i :areSame keyPressed 'd';
::restartUndo
::a lineContent :getValue currentLine 'textContent';
::a searchIndex 0
::a currentNode :o lineContent;
::a lineSize :getLength lineContent;
::i :areSame lineSize current_x;
:::e
::i :areSame current_x 0;
:::a lineContent :y lineContent;
:::setValue current_line 'innerHTML' :toHTML lineContent;
:::e
::a searchTarget :differenceNumbers current_x 1;
::i :arentSame searchIndex lineSize;
:::i :areSame searchIndex searchTarget;
::::y currentNode :y :y currentNode;; lineContent
::::setValue current_line 'innerHTML' :toHTML lineContent;
::::e
:::a currentNode :y currentNode;
:::a searchIndex :addNumbers searchIndex 1;
::e
:i :areSame keyPressed 'q';
::restartUndo
::body_node.removeChild current_line
::a current_x 0
::i :areSame current_y 0;
:::setCursor
:::current_line :addLine;
:::e
::i :areSame :getSize; current_y;
:::a current_y :differenceNumbers current_y 1;
:::setCursor
:::a current_line :getLine current_y;
:::e
::a current_line :getLine current_y;
::e
:i :areSame keyPressed 'm';
::a newLine :lineJump .5;
::a lastIndex :differenceNumbers :getSize; 1;
::i :firstGreater newLine lastIndex;
:::e
::a current_y newLine
::a current_x 0
::setCursor
::updateScroll
::e
:i :areSame keyPressed 'n';
::a newLine :lineJump .25;
::a lastIndex :differenceNumbers :getSize; 1;
::i :firstGreater newLine lastIndex;
:::e
::a current_y newLine
::a current_x 0
::setCursor
::updateScroll
::e
:i :areSame keyPressed 'o';
::a newLine :lineJump .5 true;
::i :firstGreater 0 newLine;
:::e
::a current_y newLine
::a current_x 0
::setCursor
::updateScroll
::e
:i :areSame keyPressed 'j';
::i :areSame current_y 0;
:::e
::i :areSame editing_mode true;
:::a selection_start :differenceNumbers selection_start 1;
:::highlightLine selection_start
:::e
::a previousY :differenceNumbers current_y 1;
::a current_line :getLine previousY;
::a current_y previousY
::a current_x 0
::setCursor
::e
:i :areSame keyPressed 'k';
::i :areSame current_y :differenceNumbres :getSize; 1;;
:::e
::i :areSame editing_mode true;
:::a selection_end :addNumbers selection_end 1;
:::highlightLine selection_end
:::e
::a nextY :addNumbers current_y 1;
::a current_line :getLine nextY;
::a current_y nextY
::a current_x 0
::setCursor
::e
:i :areSame keyPressed 'h';
::i :areSame current_x 0;
:::e
::a current_x :differenceNumbers current_x 1;
::setCursor
::e
:i :areSame keyPressed 'l';
::i :areSame current_x :getLength :getValue current_line 'textContent';;;f
:::e
::a current_x :addNumbers current_x 1;
::setCursor
::e
:i :areSame keyPressed 'g';
::a current_line :getLine 0;
::a current_x 0
::a current_y 0
::setCursor
::updateScroll
::e
:i :areSame keyPressed 't';
::a lastIndex :differenceNumbers :getSize; 1;
::a current_line :getLine lastIndex;
::a current_x 0
::a current_y lastIndex
::setCursor
::updateScroll
::e
:i :areSame keyPressed 's';
::localStorage.setItem storage_name :getContent;
::e
:i :areSame keyPressed 'z';
::restartUndo
::clearContent
::setContent :localStorage.getItem storage_name;
::a current_x 0
::a current_y 0
::a current_line :getLine 0;
::setCursor
::updateScroll
::e

:getElement elementID
:a editorElements :getValue body_node 'children';
:a searchIndex 0
:a numberElements :getValue editorElements 'length';
:i :firstGreater numberElements searchIndex;
::a currentElement :getValue editorElements searchIndex;
::a currentID :getValue currentElement 'id';
::i :areSame elementID currentID;
:::e currentElement
::a searchIndex :addNumbers searchIndex 1;

:updateScroll
:window.scrollTo 0 :differenceNumbers :multiplyNumbers current_y font_size; breathing_room;

:secondaryAction keyEvent
:a keyPressed :getValue keyEvent 'key';
:i :areSame keyPressed 'Escape';
::a command_mode true
::a search_mode false
::a search_term ''
::i :areSame editing_mode true;
:::a editing_mode false
:::unhighlightAll
:::e
::e

:unhighlightAll
:a searchIndex selection_start
:i :arentSame :firstGreater searchIndex selection_end; true;
::unhighlightLine searchIndex 
::a searchIndex :addNumbers searchIndex 1;

:getLength stringValue
:a currentNode :o stringValue;
:a lengthCount 0
:i :arentSame currentNode null
::a lengthCount :addNumbers lengthCount 1;
::a currentNode :y currentNode;
:e lengthCount

:currentIsland
:a lineContent :getLine current_y;
:a islandString lineContent
:a searchIndex 0
:a currentNode :o lineContent;
:i :arentSame currentNode null;
::i :areSame current_x searchIndex;
:::a currentNode :y currentNode;
:::i :arentSame currentNode null;
::::i :areSame :u currentNode; ' ';
:::::y previousNode null islandString
:::::e islandString
::::a previousNode currentNode
::::a currentNode :y currentNode;
:::e islandString
::i :areSame :u currentNode ':';
:::a islandString :y currentNode;
::a currentNode :y currentNode;
::a searchIndex :addNumbers searchIndex 1;

:findNext searchTerm currentY
:a nextIndex currentY
:a searchIndex :addNumbers currentY 1;
:a numberLines :getSize;
:i :arentSame searchIndex numberLines;
::i :areSame true :lineContains :getValue :getLine searchIndex; 'textContent'; searchTerm;;
:::e searchIndex  
::a searchIndex :addNumbers searchIndex 1;
:e nextIndex

:lineContains lineContent searchTerm
:a firstCharacter :u searchTerm;
:a currentNode :o lineContent;
:i :arentSame currentNode null;
::i :areSame :u currentNode; firstCharacter;
:::a string1 :y currentNode;
:::a string2 :y searchTerm;
:::i :areSame :u string1; :u string2;;
::::a string1 :y string1;
::::a string2 :y string2;
::::i :areSame string1 null;
:::::i :areSame string2 null;
::::::e true
::::e false
:::e lineContains :y currentNode; searchTerm 
::a currentNode :y currentNode;
:e false
